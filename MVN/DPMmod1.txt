model{

  for (i in 1:N1)
{ y[i,1]~dnorm(mu1[group[i]],tau1[group[i]])
  group[i]~dcat(p[])		
	for (l in 1:K) {	gind[i,l]<-equals(l,group[i])	
logli0[i,l]<-1/sqrt(2*3.14)*sqrt(tau1[l])*exp(-0.5*tau1[l]*pow((y[i,1]-mu1[l]),2))
}
logli[i]<-log(inprod(p[1:K],logli0[i,1:K]))
} 

for (i in (N1+1):N2)
{ y[i,1]~dnorm(mu1[group[i]],tau1[group[i]])
  mu22[i]<-beta0[group[i]]+phi21[group[i]]*y[i,1]
  y[i,2]~dnorm(mu22[i],tau2[group[i]])
  group[i]~dcat(p[])		
	for (l in 1:K) {	gind[i,l]<-equals(l,group[i])	
	mucls22[i,l]<-beta0[l]+phi21[l]*y[i,1]
logli0[i,l]<-(1/sqrt(2*3.14)*sqrt(tau1[l]))*exp(-0.5*tau1[l]*pow((y[i,1]-mu1[l]),2))*
    1/sqrt(2*3.14)*sqrt(tau2[l])*exp(-0.5*tau2[l]*pow((y[i,2]-mucls22[i,l]),2))
}
logli[i]<-log(inprod(p[1:K],logli0[i,1:K]))
} 

for (i in (N2+1):N)
{ y[i,1]~dnorm(mu1[group[i]],tau1[group[i]])
  mu22[i]<-beta0[group[i]]+phi21[group[i]]*y[i,1]
  mu33[i]<-beta1[group[i]]+phi31[group[i]]*y[i,1]+phi32[group[i]]*y[i,2]
  y[i,2]~dnorm(mu22[i],tau2[group[i]])
  y[i,3]~dnorm(mu33[i],tau3[group[i]])
  group[i]~dcat(p[])		
	for (l in 1:K) {	gind[i,l]<-equals(l,group[i])	
	mucls22[i,l]<-beta0[l]+phi21[l]*y[i,1]
    mucls33[i,l]<-beta1[l]+phi31[l]*y[i,1]+phi32[l]*y[i,2]
	logli0[i,l]<-1/sqrt(2*3.14)*sqrt(tau1[l])*exp(-0.5*tau1[l]*pow((y[i,1]-mu1[l]),2))*
    1/sqrt(2*3.14)*sqrt(tau2[l])*exp(-0.5*tau2[l]*pow((y[i,2]-mucls22[i,l]),2))*
    1/sqrt(2*3.14)*sqrt(tau3[l])*exp(-0.5*tau3[l]*pow((y[i,3]-mucls33[i,l]),2))
}
logli[i]<-log(inprod(p[1:K],logli0[i,1:K]))
} 


p[1]<-v[1]
for (l in 2:K) {	
	p[l] <- v[l]*(1-v[l-1])*p[l-1]/v[l-1]
	v[l-1] ~ dbeta(1,alpha)T(,0.99)
}
v[K] <- 1


for (l in 1:K) {	
phi21[l]~dnorm(phi210, tau.phi21)
phi31[l]~dnorm(phi310, tau.phi31)
phi32[l]~dnorm(phi320, tau.phi32)
mu1[l]~dnorm(mu10,tau.mu1)
beta0[l]~dnorm(beta00, tau.beta0)
beta1[l]~dnorm(beta10, tau.beta1)
sigma1[l]~dunif(sigma10,sigma11)
sigma2[l]~dunif(sigma20,sigma21)
sigma3[l]~dunif(sigma30,sigma31)
tau1[l]<-1/pow(sigma1[l],2)
tau2[l]<-1/pow(sigma2[l],2)
tau3[l]<-1/pow(sigma3[l],2)
mu2[l]<-beta0[l]+phi21[l]*mu1[l]
mu3[l]<-beta1[l]+phi31[l]*mu1[l]+phi32[l]*mu2[l]
}

smu3[1]<-p[1]*mu3[1]
smu2[1]<-p[1]*mu2[1]
smu1[1]<-p[1]*mu1[1]
for (l in 2:K) {
smu1[l]<-smu1[l-1]+p[l]*mu1[l]
smu2[l]<-smu2[l-1]+p[l]*mu2[l]
smu3[l]<-smu3[l-1]+p[l]*mu3[l]
}

mmu1<-smu1[K]
mmu2<-smu2[K]
mmu3<-smu3[K]

############## prior

alpha~dgamma(1,1)
phi210~dnorm(0.9, 1/0.04)
phi310~dnorm(0.2, 1/0.04)
phi320~dnorm(0.65, 1/0.04)
mu10~dnorm(65,1/16)
beta00~dnorm(15, 1/100)
beta10~dnorm(12, 1/100)
tau.phi21<-1/pow(sigma.phi21,2)
tau.phi31<-1/pow(sigma.phi31,2)
tau.phi32<-1/pow(sigma.phi32,2)
tau.mu1<-1/pow(sigma.mu1,2)
tau.beta0<-1/pow(sigma.beta0,2)
tau.beta1<-1/pow(sigma.beta1,2)
sigma.phi21~dunif(0.05,0.25)
sigma.phi31~dunif(0.1,0.25)
sigma.phi32~dunif(0.1,0.25)
sigma.mu1~dunif(3,5)
sigma.beta0~dunif(6,16)
sigma.beta1~dunif(5,12)
sigma10~dunif(18,22)
sigma20~dunif(10,17)
sigma30~dunif(6,12)
sigma11~dunif(29,35)
sigma21~dunif(17,32)
sigma31~dunif(12,22)


cluster<-sum(cl[])
for (l in 1:K){
	sumind[l]<-sum(gind[,l])
	cl[l]<-step(sumind[l]-1+0.001)
}

}
